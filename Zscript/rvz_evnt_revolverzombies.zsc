class RevolverZombieEvent : eventhandler
{


	private bool cvarsAvailable;

	private int  spawnBiasActual;
	private int  semisSpawnBiasActual;
	private int  smgsSpawnBiasActual;
	private int  corpseSpawnBiasActual;
	private int  spawn355BiasActual;
	private bool isPersistent;
	
	
	// Shoves cvar values into their non-cvar shaped holes.
	// I have no idea why names for cvars become reserved here.
	// But, this works. So no complaints. 
	void init()
	{
		cvarsAvailable = true;
		spawnBiasActual       = rvz_autos_spawn_bias;
		semisSpawnBiasActual  = rvz_semis_spawn_bias;
		smgsSpawnBiasActual   = rvz_corpse_spawn_bias;
		corpseSpawnBiasActual = rvz_smgs_spawn_bias;
		spawn355BiasActual    = rvz_355_chance_bias;
		isPersistent          = rvz_persistent_spawning;
	}
	
	
	// 'Initalizes' the event handler,
	// In my testing, this is called after events are fired. 
	override void WorldLoaded(WorldEvent e)
	{
		// always calls init.
		init();
		super.WorldLoaded(e);
	}

	bool giverandom(int chance)
	{
		bool result = false;
		
		// temp storage for the random value. 
		int iii = random(0, chance);
		
		// force negative values to be 0. 
		if(iii < 0)
			iii = 0;
			
		
		if (iii == 0)
		{
			if(chance > -1)
				result = true;
		}
		
		return result;
	}



void trycreatecorpse(worldevent e, int chance)
	{
		if(giverandom(chance))
		{
			let zzz = RevolverZombieman(e.thing.Spawn("RevolverZombiemanCorpse", e.thing.pos, SXF_TRANSFERSPECIAL | SXF_NOCHECKPOSITION));
			
			if(zzz)
			{
				if(giverandom(spawn355BiasActual))
				{
					zzz.A_setpreference(true);
				}
				else
				{
					zzz.A_setpreference(false);
				}
				zzz.A_HDWRevolverRandomizeAmmo();
				// Copy the original actor's angle.
				zzz.angle = e.thing.angle;
				// Remove the original mob. 
				e.thing.destroy();

		}
		}
	}


	void trycreatezombie(worldevent e, int chance)
	{
		if(giverandom(chance))
		{
			let zzz = RevolverZombieman(e.thing.Spawn("RevolverZombieman", e.thing.pos, SXF_TRANSFERSPECIAL | SXF_NOCHECKPOSITION));
			
			if(zzz)
			{
				if(giverandom(spawn355BiasActual))
				{
					zzz.A_setpreference(true);
				}
				else
				{
					zzz.A_setpreference(false);
				}
				zzz.A_HDWRevolverRandomizeAmmo();
				// Copy the original actor's angle.
				zzz.angle = e.thing.angle;
				// Remove the original mob. 
				e.thing.destroy();

		}
		}
	}


override void worldthingspawned(worldevent e)
  {
	// Makes sure the values are always loaded before
	// taking in events.
	if(!cvarsAvailable)
		init();
		
 	// in case it's not real. 
	if(!e.Thing)
	{
		return;
	}
	
	// Don't spawn anything if the level has been loaded more than a tic. 
	if (!(level.maptime > 1) || isPersistent)
	{
		switch(e.Thing.GetClassName())
		{
			case 'ZombieAutoStormtrooper':
				trycreatezombie(e, spawnBiasActual);
				break;
			case 'ZombieSemiStormtrooper':
				trycreatezombie(e, semisSpawnBiasActual);
				break;
			case 'ZombieSMGStormtrooper':
				trycreatezombie(e, smgsSpawnBiasActual);
				break;
			case 'DeadZombieStormtrooper':
				trycreatecorpse(e, corpseSpawnBiasActual);
		}
	}
	}
}