class BiomechanoidPlasma : hdfireball
{

	default
	{
		Radius 5;
		missileheight 5;
		Speed 35;
		Damage 1;
		gravity 0;
		Damagetype "electrical";
		RenderStyle "translucent";
		scale 0.4;
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		activesound "weapons/plasmax";
		obituary "%o was cooked to perfection.";
	}
	
	States
	{
		Spawn:
			PLSE A 6 Bright;
		Tryspark:
			#### #  0 A_Jump(78, "Spark");
			goto spawn;
			
		Spark:
			#### B 0 Bright
			{
				for(int i = 0; i < 2; i++)
				{
					A_StartSound("misc/arczap",volume:0.3,attenuation:2.);
					A_CustomRailgun((random(1,4)),0,"","whiteblue",
					RGF_SILENT|RGF_NOPIERCING|RGF_FULLbright|RGF_CENTERZ,
					0,4000,"HDArcPuff",180,180,random(60,160),18,1.4,1.5
					);
				}
			}
			goto Spawn;
			
		Death:
			PLSE ABCDE 4 Bright
			{
				for(int i = 0; i < 3; i++)
				{
					A_StartSound("misc/arczap",volume:0.3,attenuation:2.);
					A_CustomRailgun((random(1,4)),0,"","whiteblue",
					RGF_SILENT|RGF_NOPIERCING|RGF_FULLbright|RGF_CENTERZ,
					0,4000,"HDArcPuff",180,180,random(60,160),18,1.4,1.5
					);
				}
				//A_SprayDecal("BusterScorch",14);
			}
			Stop;
	}
}



class BiomechanoidHeatBlast : BiomechanoidPlasma
{

	actor lingerburner;
	default
	{
		RenderStyle "add";
		Alpha 0.9;
		Speed 22;
		missiletype "SquireFireBallTrail";
		translation "128:143=60:79" , "192:207=210:223";
		activesound "misc/firecrkl";
		gravity 0;
		hdfireball.firefatigue int(HDCONST_MAXFIREFATIGUE*0.1);
	}
	
	States
	{
		Spawn:
			PLSE A 6 Bright;
			loop;
			
		Death:
			#### # 0
			{
				vel.z+=0.5;
				if(!blockingmobj)
				{
					tracer=null;
					setstatelabel("burn");
					lingerburner=spawn("BioMechHeatBurner",pos,ALLOW_REPLACE);
					lingerburner.target=target;lingerburner.master=self;
					return;
				}
				else if(target&&
					blockingmobj.health>0&&target.health>0&&
					blockingmobj.getspecies()==target.getspecies()&&
					!(blockingmobj.ishostile(target))
				)return;
				else if(tracer&&blockingmobj==tracer)
				{
					vel.z=2;
					tracer.damagemobj(self,target,10 + random(3,6),"hot");
					alpha=1;scale*=1.2;
					setstatelabel("burn");
				}
			}
		Blam:
			PLSE ABCDE 4 Bright;
			Stop;
	burn:
		PLSE CDE 3
		{
			A_FadeOut(0.10);
			frame=random(2,4);roll=random(0,360);
			if(!tracer){
				addz(0.1);
				return;
			}

			if(tracer is "HDPlayerPawn"&&tracer.health<1&&HDPlayerPawn(tracer).playercorpse){
				tracer=HDPlayerPawn(tracer).playercorpse;
			}

			double trad=tracer.radius;double tht=tracer.height;
			setxyz(tracer.pos+(frandom(-trad,trad),frandom(-trad,trad),frandom(tht*0.5,tht)));

			if(alpha>0.3)
			{
				tracer.damagemobj(self,target,random(1,3),"electrical");
			}
			else
			{
				tracer.damagemobj(self,target,random(4,6),"electrical");
				A_SprayDecal("BrontoScorch",radius*2);
				destroy();
			}
		}
		wait;
	}
}



class BioMechHeatBurner : PersistentDamager
{
	default
	{
		height 12;radius 20;stamina 1;
		damagetype "hot";
	}
}