extend class HDRERevolver
{


	// Initalizes private variables. 
	override void postbeginplay()
	{
		super.postbeginplay();
		__cylinder_i = 0;
		__reload_i   = 0;
		__unloaded   = false;
		__cocked     = false;
	}

	
	override void deathdrop()
	{
		// Only drops a weapon once. 
		if(!bhasdropped)
		{
			// Spawns a brand new revolver. 
			let www=DropNewWeapon("HDRevolver");
			int i = 6;
			if(www)
			{
				// Populates the revolver array.
				for(int j = 0; j < REV_SIZE; j++)
				{
					if(__cylinder_i-j >= 0)
					{
						switch(__cylinder[__cylinder_i-j])
						{
							case REV_9MM:
								www.weaponstatus[i] = 2;
								break;
							case REV_355:
								www.weaponstatus[i] = 4;
								break;
							case REV_9MM_SPENT:
								www.weaponstatus[i] = 1;
								break;
							case REV_355_SPENT:
								www.weaponstatus[i] = 3;
								break;
							default:
								www.weaponstatus[i] = 0;
								break;
						
						}
					}
					else
					{
						www.weaponstatus[i] = 0;
					}
					i--;
				}
			}
			// Drops random ammo from freshly killed zombies. 
			if(!random(0,6))
			{
				if(pref355)
				{
					A_DropAmmo('HDRevolverAmmo', 10, 24);
				}
				else
				{
					A_DropAmmo('HDPistolAmmo', 10, 24);
				}
			}
		}
		// Always gives ammo for resurrected zombies. 
		else if(bhasdropped)
		{
			if(pref355)
			{
				A_DropAmmo('HDRevolverAmmo', 10, 24);
			}
			else
			{
				A_DropAmmo('HDPistolAmmo', 10, 24);
			}
		}
		Super.Deathdrop();
	}
}