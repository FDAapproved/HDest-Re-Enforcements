//------------------------------------------------------------------------------
// The warmest place to hide... is Frag. 
//------------------------------------------------------------------------------

// For mappers. Uncomment and add thingids as needed.
/*
class InnocentMimicBarrel : HDMimicBarrel
{
	default
	{
		hdbarrel.musthavegremlin -1;
		mimicbarrel.tampered = 1;
	}
}


class KillerMimicBarrel : HDMimicBarrel
{
	default
	{
		hdbarrel.musthavegremlin 1;
		mimicbarrel.tampered = 1;
	}
}
*/



class MimicBarrel : HDBarrel
{ 
//$Category "Monsters/Hideous Destructor"
//$Title "Mimic Barrel"
//$Sprite "BAR1A0"
	  int timetowait;
	  int time_i;
	  int lastshouted;
	  
	  // For controlling disguises and wakeup.
	  bool isamogus;
	  bool hasawaken;
	  
	  // Inherited from cvars, control whether barrels are easy to spot. 
	  bool isobvious;
	  bool tampered;
	  
	  // see above. 
	  transient cvar c_balespawnbias;
	  transient cvar c_isobvious;
	  
	  default
	  {
			+solid +shootable
			+activatemcross +canpass +nodropoff
			+fixmapthingpos +dontgib
			+hdmobbase.doesntbleed
			-countkill
			-ismonster
			+hdmobbase.noshootablecorpse
			+hdmobbase.climber
			damagefactor "hot",1.2;
			damagefactor "cold",1.2;
			damagefactor "Balefire",0.1;
			radius 11;height 34;
			speed 12;
			health 100;mass 200;gibhealth 200;
			maxdropoffheight 48;
			maxstepheight 4;
			painchance 256;
			pushfactor 0.1;
			meleethreshold 20;
			deathsound "bmimic/death";
			translation "176:191=96:111","64:79=104:111";
			painsound "bmimic/pain";
			bloodcolor "ba ff 86";bloodtype "NotQuiteBloodSplat";
			obituary "%o got cream of canned!";
			
			// Not evil at all :) I promise. 
			tag "barrel";
	   }
	  
	// OR together any extra cvars for this check. We just want to know if it exists.
	virtual bool has_cvars()
	{
		return c_isobvious;
	}
	  
	virtual void get_cvars()
	{
		 c_isobvious = cvar.getcvar("brl_obviousness");
		 isobvious = c_isobvious.getBool();
	}

	  
	// So monsters have their own field for postbeginplay info
	// SPECIFIC to them. This prevents inheritance from getting messy. 
   virtual void pb_configure()
   {
   		if(!tampered)
		{
			c_balespawnbias = cvar.getcvar("brl_barrel_bale_bias");
			int compare   = c_balespawnbias.getInt();
			
			if(HDREUtility.giveRandomBool(compare))
			{
				musthavegremlin = 1;
			}
			else if (compare == -1)
			{
				musthavegremlin = -1;
			}
			else if (compare != -2)
			{
				musthavegremlin = -1;
			}
		}
   }
   
   // Attaches a dynsmic light.
   override void postbeginplay()
   {
		super.postbeginplay();
		hasawaken = false;
		self.pb_configure();
   }
   
 
   override void tick()
   {
	// Behavior Cvar loader. These values are shunted into other booleans. 
	if(self.has_cvars() != true)
		self.get_cvars();
	
	// Regular Tick()
	super.tick();
	
	// Abort if time is frozen.
	if(isFrozen())return;
	
    // Forces barrels to persue you.
	bfrightened=false;
	
	
	// Occasionally screams.
	// Only ever is fully called whenever lastshouted is 0;
	if(health > 0 && !lastshouted && vel != (0,0,0) && !(random(0, 150)))
	{
		A_StartSound("bmimic/pain",CHAN_BODY);
		lastshouted = 106;
	}
	
	// Resets lastshouted to 0 after 106-1 ticks. 
	if(lastshouted)lastshouted = lastshouted-1;
	
   }
   
   virtual void A_SusImpostor()
   {  
		if(!isobvious)
		{
			self.isamogus=false;
			self.setstatelabel("Spawn");
			return;
		}
		if(!isamogus)
			self.isamogus=true;
			
		if(hasawaken)
			self.hasawaken=false;
   }
   
   virtual void A_TryTwitch(int chance)
   {
		if(!(random(0, chance)) && vel == (0,0,0))
		{
			self.A_MimicBarrelMove();
			
			// redisguise sometimes (tucks it's legs back in).
			if(!isobvious && random(0,1))
			{
				self.isamogus = false;
				self.setstatelabel("Impostor");
			}
		}
   }
   
   // In case this class ever needs it. 
   	void A_MimicBarrelCheckCrowd()
	{
		int bcc=0;
		barrelexplodemarker bpm;
		thinkeriterator bexpm=ThinkerIterator.create("BarrelExplodeMarker");
		while(bpm=barrelexplodemarker(bexpm.next(true))){
			bcc++;
			if(bcc>random(1,12)){
				setstatelabel("waittoexplode");
				break;
			}
		}
	}



	// Leaping and pouncing functions. 
 	void A_MimicBarrelMove()
	{
		if(floorz!=pos.z)return;
		A_FaceTarget();
		angle+=random(-150,150);
		A_StartSound("barrel/walk",CHAN_BODY);
		vel+=(cos(angle),sin(angle), 0) * random(1,4);
		vel+=(0,0, frandom(0, 1) * 2);
		setstatelabel("Jumped");
	}
 	void A_MimicBarrelMoveKill()
	{
		if(floorz!=pos.z)return;
		A_FaceTarget();
		angle+=random(-15,15);
		A_StartSound("barrel/walk",CHAN_BODY);
		vel+=(cos(angle),sin(angle), 0) * 3;
		vel+=(0,0,2);
	}
	
  States
  {
	  // Seeming sus lately... 
	  Impostor:
			#### # 0
			{
				// Are you a monster??? 
				invoker.A_SusImpostor();
			}
	  ImpostorIdle:
			BALE AE 10;
			BALE E 0
			{
				invoker.A_TryTwitch(150);
			}
			Loop;


	  // Removed 'look' since if this could look, it'd see and then run at you. 
	  Spawn:
			BAR1 A 0 NODELAY;
			#### # 0
			{
				if(time_i != 0)setstatelabel("AwakenLoop");
				if(hasawaken)hasawaken=false;
				// If a barrel has been found out, it continues to be as such.
				// There's a % chance this won't happen.
				if(isobvious || isamogus)setstatelabel("Impostor");
			}
	  SpawnIdle:
			BAR1 AB 10;
			BAR1 B 0
			{
				if(!(random(0, 150)) && vel == (0,0,0))
				{
					A_MimicBarrelMove();
				}
			}
			Loop;
	  // Transitions the barrel into living (sicko) mode. 
	  Awaken:
			BALE A 0
			{
				timetowait = (invoker.health/4);
				timetowait = random(timetowait, timetowait*2);
				time_i     = 0;
				A_Recoil(frandom(-0.80, 0.80));
			}
	 AwakenLoop:
			BALE AE 4
			{
				time_i++;
			}
			BALE E 0 A_JumpIf(time_i < timetowait, "AwakenLoop");
			BALE B 1
			{
				hasawaken = true;
				A_HDLook();
				if(!target)setstatelabel("Gaze");
			}
			goto See;
	  
	  Jumped:
			BALE C 2
			{
				invoker.frame+=random(-1,1);
			}
			goto impostor;
	  See:
		BALE BCD 3
		{

			if(target)
			{
				// Update target pointer. 
				A_HDLook();
				
				// Pounce!
				if(Distance3D(target)<=meleethreshold && !random(0,5))
				{
					setstatelabel("Explode");
				}
				
				A_HDChase(meleestate: "Explode",speedmult:frandom(1,1.4));
			}
			else
			{
				setstatelabel("Gaze");
			}

		}
		BALE D 0 A_JumpIfTargetInLOS("see");
		BALE D 0 A_Jump(110, "see");
		BALE D 0 A_Jump(30, "spawn");
		goto gaze;
		
	 Gaze:
		BALE BCD 3 A_HDLook();
		BALE D   1
		{
			if(target)
				setstatelabel("see");
			else
				A_Jump(110, "spawn");
		}
		loop;
	 
	  Explode:
		BALE A 1 DamageThing(invoker.health,0);
		goto death;
	  Pain:
		#### # 0
		{
			A_Pain();
			if(!hasawaken)
			{
				if(target)
				{
					// Kills you if you're dragging the barrel.
					if(vel != (0,0,0) && Distance3D(target) <= 10)
					{
						setstatelabel("Explode");
						return;
					}
				}
				
				setstatelabel("Awaken");
			}
		}
		BALE C 2
		{
			invoker.frame+=random(-1,1);
			
			// Leap.
			if(target)
				A_MimicBarrelMoveKill();
			
			// See you in hell!
			if(target && (Distance3D(target)<=meleethreshold*frandom(1,1.2)) && !random(0,10))
			{
				setstatelabel("Explode");
			}
		}
		BALE C 2
		{
			invoker.frame+=random(-1,1);
		}
		Goto See;
		
	// Death states from the normal barrel, minus the random explosion delay. 
	death:
		BALE A 0;
		---- A 0 A_NoBlocking();
		---- A 0 A_SetSize(-1,default.height);
	waittoexplode:
		---- A 0 A_SetSolid();
		---- A 0 A_SetShootable();
		---- AAAAAA random(1,2);
		---- A random(1,2);
	reallyexplode:
		BEXP C 0
		{
			A_UnsetSolid();
			A_UnsetShootable();
			A_NoBlocking();
			A_SetSize(-1,deathheight);
			bsolid=false;
			new("BarrelExplodeMarker");

			//set counter for spawning chunks in a crowd
			stamina=0;
			blockthingsiterator a=blockthingsiterator.create(self,320);
			while(a.next()){
				if(
					a.thing is "BarrelExplodeMarker"
				){
					stamina++;
				}
			}

			//A_StartSound("world/barrelx");
			actor xpl=spawn("HDExplosionLight",pos,ALLOW_REPLACE);
			xpl.target=self;xpl.stamina=256;
		}
		BEXP CCC 1 bright{
			if(stamina<3){
				for(int i=0;i<16;i++){
					actor aaa=spawn("HugeWallChunk",(pos.xy,pos.z+frandom(0,height)),ALLOW_REPLACE);
					aaa.vel=(frandom(-10,10),frandom(-10,10),frandom(4,20));
					if(aaa.vel dot aaa.vel < 100)
						aaa.A_SetTranslation((KillerFireCan(self)||random(0,2))?"Charred":"Booger");
					aaa.scale*=frandom(0.8,1.8);
				}
			}
			actor a=spawn("HDExplosion",self.pos+(random(-2,2),random(-2,2),random(12,32)),ALLOW_REPLACE);
			a.vel=self.vel+(random(-1,1),random(-1,1),random(-1,2));
		}
		BEXP C 0 A_BarrelBlast();
		BEXP D 2 bright{
			A_SpawnChunks("HDSmokeChunk",random(2,5),7,16);
			A_Scream();
			DistantQuaker.Quake(self,6,42,512,10);
			A_StartSound("world/explode");
			DistantNoise.Make(self,"world/rocketfar");
		}
		BEXP EEEEEEE 0 A_SpawnItemEx ("HDSmoke", random(-6,6),random(-6,6),random(12,32), vel.x+random(-1,1),vel.y+random(-1,1),vel.z+random(1,2), 0,168);
		BEXP EEE 2 bright A_FadeOut(0.3);
		POB1 A 0
		{
			A_SetSize(-1,8);
			A_SetScale(0.7);
			A_SetRenderstyle(1,Style_Normal);
			setstatelabel("dying");
			A_GiveInventory("Heat",6660);
			bshootable=true;
			bpushable=false;
			bbright=false;
		}
	dying:
		POB1 AAAAAAA 50{
			A_Immolate(self,self.target,random(12,24));
			if(!random(0,2)) A_SpawnChunks("HDSmokeChunk",1,2,12);
		}
		POB1 A -1{
			A_StartSound("vile/firestrt",CHAN_AUTO,volume:0.4);
			A_SpawnChunks("HDSmoke",8,0,3);
		}stop;
	}
}