class PistolZombieEvent : eventhandler
{


	private bool cvarsAvailable;

	private int  spawnBiasActual;
	private int  semisSpawnBiasActual;
	private int  smgsSpawnBiasActual;
	private int  corpseSpawnBiasActual;
	private int  spawnFiremodeBiasActual;
	private bool isPersistent;
	
	
	// Shoves cvar values into their non-cvar shaped holes.
	// I have no idea why names for cvars become reserved here.
	// But, this works. So no complaints. 
	void init()
	{
		cvarsAvailable = true;
		spawnBiasActual         = psz_autos_spawn_bias;
		semisSpawnBiasActual    = psz_semis_spawn_bias;
		smgsSpawnBiasActual     = psz_smgs_spawn_bias;
		corpseSpawnBiasActual   = psz_corpse_spawn_bias;
		spawnFiremodeBiasActual = psz_firemode_chance_bias;
		isPersistent            = psz_persistent_spawning;
	}
	
	
	// 'Initalizes' the event handler,
	// In my testing, this is called after events are fired. 
	override void WorldLoaded(WorldEvent e)
	{
		// always calls init.
		init();
		super.WorldLoaded(e);
	}

	bool giverandom(int chance)
	{
		bool result = false;
		
		// temp storage for the random value. 
		int iii = random(0, chance);
		
		// force negative values to be 0. 
		if(iii < 0)
			iii = 0;
			
		
		if (iii == 0)
		{
			if(chance > -1)
				result = true;
		}
		
		return result;
	}



void trycreatecorpse(worldevent e, int chance)
	{
		if(giverandom(chance))
		{
			let zzz = PistolZombieman(e.thing.spawn("PistolZombiemanCorpse", e.thing.pos, SXF_TRANSFERSPECIAL | SXF_NOCHECKPOSITION));
			
			if(zzz)
			{
				if(giverandom(spawnFiremodeBiasActual))
				{
					zzz.A_setfiremode(true);
				}
				else
				{
					zzz.A_setfiremode(false);
				}
				
				// Remove the original mob. 
				e.thing.destroy();

		}
		}
	}


	void trycreatezombie(worldevent e, int chance)
	{
		if(giverandom(chance))
		{
			
			let zzz = PistolZombieman(e.thing.spawn("PistolZombieman", e.thing.pos, SXF_TRANSFERSPECIAL | SXF_NOCHECKPOSITION));
			
			if(zzz)
			{
				if(giverandom(spawnFiremodeBiasActual))
				{
					zzz.A_setfiremode(true);
				}
				else
				{
					zzz.A_setfiremode(false);
				}
				// Copy the original actor's angle.
				zzz.angle = e.thing.angle;
				
				// Remove the original mob. 
				e.thing.destroy();

		}
		}
	}


override void worldthingspawned(worldevent e)
  {
	// Makes sure the values are always loaded before
	// taking in events.
	if(!cvarsAvailable)
		init();
		
 	// in case it's not real. 
	if(!e.Thing)
	{
		return;
	}
	
	// Don't spawn anything if the level has been loaded more than a tic. 
	if (!(level.maptime > 1) || isPersistent)
	{
		switch(e.Thing.GetClassName())
		{
			case 'ZombieAutoStormtrooper':
				trycreatezombie(e, spawnBiasActual);
				break;
			case 'ZombieSemiStormtrooper':
				trycreatezombie(e, semisSpawnBiasActual);
				break;
			case 'ZombieSMGStormtrooper':
				trycreatezombie(e, smgsSpawnBiasActual);
				break;
			case 'DeadZombieStormtrooper':
				trycreatecorpse(e, corpseSpawnBiasActual);
		}
	}
	}
}